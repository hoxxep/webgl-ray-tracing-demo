<html>
<head>
    <meta charset="utf-8">
    <meta name="viewport" content="width=device-width, initial-scale=1">
    <title>WebGL Ray Tracer Demo - Liam Gray</title>
    <link rel="stylesheet" type="text/css" href="css/main.css">
</head>
<body>
<div id="page" class="ui container">
    <div class="ui stackable relaxed grid">
        <div class="row">
            <!-- canvas image -->
            <div class="sixteen wide mobile ten wide tablet ten wide computer column">
                <div id="screen" class="ui segment">
                    <div class="loading ui active inverted dimmer">
                        <div class="ui text loader">
                            Loading...
                        </div>
                    </div>
                    <div class="error ui inverted dimmer">
                        <div class="content">
                            <div class="center">
                                <h3>Error</h3>
                                <p>Your browser does not support WebGL.</p>
                            </div>
                        </div>
                    </div>
                    <a class="ui teal ribbon icon label" href="https://github.com/hoxxep/webgl-ray-tracing-demo"><i class="github icon"></i> WebGL Ray Tracing Demo</a>
                    <canvas class="render"></canvas>
                </div>
                <p><strong>Note:</strong> I have no idea how this page looks in IE.</p>
            </div>

            <!-- controls column -->
            <div id="controls" class="six wide column">
                <div class="profiling ui inverted dimmer">
                    <div class="ui text loader">
                        Profiling...
                    </div>
                </div>

                <h2>WebGL Ray Tracing</h2>
                <br>
                <div class="ui slider range">
                    <strong>Spheres:</strong> <span class="value"></span>
                    <input class="spheres setting" type="range" min="1" max="64" step="1" value="27">
                </div>
                <br>
                <div class="ui slider range">
                    <strong>Reflection Depth:</strong> <span class="value"></span>
                    <input class="reflections setting" type="range" min="0" max="10" step="1" value="3">
                </div>
                <br>
                <div class="ui slider range">
                    <strong>Zoom Depth:</strong> <span class="value"></span>
                    <input class="zoom setting" type="range" min="1" max="10" step="1" value="5">
                </div>
                <br>
                <div class="ui slider checkbox shadows setting" style="display: block;">
                    <input type="checkbox">
                    <label><strong>Shadows</strong></label>
                </div>

                <br>

                <button class="profile ui labeled icon gray button">
                    <i class="area chart icon"></i>
                    Profile
                </button>

                <button class="render ui right labeled icon teal button">
                    Render
                    <i class="play icon"></i>
                </button>

                <span><i class="clock icon"></i></span><span class="render-time"></span>
            </div>
        </div>
        <div class="row">
            <div class="center aligned column">
                <p class="copyright">
                    Copyright &copy; 2016 - Liam Gray
                </p>
            </div>
        </div>
    </div>
</div>

<script id="fragment-shader" type="x-shader/x-fragment">
  precision mediump float;

varying vec3 vPosition;
uniform vec3 cameraPosition;
uniform int reflections;  // max = 10
uniform bool shadows;
uniform int numberOfSpheres;  // max = 64
uniform vec3 sphereCenters[64];
vec3 lightDirections[3];

/**
 * Check for an intersection with a sphere
 */
bool intersectSphere(vec3 sphereCenter, vec3 rayStart, vec3 rayDirection, out float distance) {
  vec3 rayToSphere = sphereCenter - rayStart;
  float b = dot(rayDirection, rayToSphere);
  float d = b*b - dot(rayToSphere, rayToSphere) + 1.0;

  if (d < 0.0) {
    distance = 10000.0;
    return false;
  }

  distance = b - sqrt(d);
  if (distance < 0.0) {
    distance = 10000.0;
    return false;
  }

  return true;
}

/**
 * Does the ray intersect a sphere, if so, output the sphere's index and distance from the ray start
 */
bool intersectSpheres(vec3 rayStart, vec3 rayDirection, out int sphereIndex, out float distance, out vec3 intersectPosition, out vec3 normal) {
  float minDistance = -1.0, thisDistance = 0.0;
  for (int i = 0; i < 64; i++) {
    if (i < numberOfSpheres) {
      if (intersectSphere(sphereCenters[i], rayStart, rayDirection, thisDistance)) {
        if (minDistance < 0.0 || thisDistance < minDistance) {
          minDistance = thisDistance;
          sphereIndex = i;
          intersectPosition = rayStart + minDistance * rayDirection;
          normal = intersectPosition - sphereCenters[i];
        }
      }
    }
  }

  if (minDistance <= 0.0) {
    sphereIndex = -1;
    distance = 10000.0;
    return false;
  } else {
    distance = minDistance;
    return true;
  }
}

bool intersectSpheresSimple(vec3 rayStart, vec3 rayDirection) {
  float minDistance = -1.0, thisDistance = 0.0;
  for (int i = 0; i < 64; i++) {
    if (i < numberOfSpheres) {
      if (intersectSphere(sphereCenters[i], rayStart, rayDirection, thisDistance)) {
        if (minDistance < 0.0 || thisDistance < minDistance) {
          minDistance = thisDistance;
        }
      }
    }
  }

  return (minDistance >= 0.0);
}

/**
 * Calculate the intensity of light at a certain angle - 0.0 means none, 1.0 means true colour, >1.0 for gloss/shine
 */
vec3 lightAt(vec3 position, vec3 normal, vec3 viewer, vec3 color) {
  vec3 light = lightDirections[0];
  vec3 reflection = reflect(-light, normal);

  vec3 intersectPosition, n;
  float intensity = 0.0, distance;
  int sphereIndex;

  for (int i = 0; i < 3; i++) {
    light = lightDirections[i];
    reflection = reflect(-light, normal);

    // TODO: check if testing for shadows here is valid...
    if (!shadows || !intersectSpheresSimple(position, light)) {
      intensity = intensity + 0.05 + 0.3 * pow(max(dot(reflection, viewer), 0.0), 30.0) + 0.7 * dot(light, normal);
    } else {
      intensity = intensity + 0.05;
    }
  }

  intensity = intensity / 1.5;

  if (intensity > 1.0) {
    return mix(color, vec3(1.2, 1.2, 1.2), intensity - 1.0);
  }

  return intensity * color;
}

/**
 * Check if our ray intersects with an object/floor
 */
bool intersectWorld(vec3 rayStart, vec3 rayDirection, out vec3 intersectPosition, out vec3 normal, out vec3 color) {
  int sphereIndex;
  float distance;

  if (intersectSpheres(rayStart, rayDirection, sphereIndex, distance, intersectPosition, normal)) {
    float i = float(sphereIndex);
    float n = i / 32.0;
    color = vec3(sin(1.0/n) / 2.0 + 0.5, sin(n) / 2.0 + 0.5, cos(n) / 2.0 + 0.5);
  } else if (rayDirection.y < -0.01) {
    intersectPosition = rayStart + ((rayStart.y + 2.7) / -rayDirection.y) * rayDirection;

    if (intersectPosition.x*intersectPosition.x + intersectPosition.z*intersectPosition.z > 300.0) {
      return false;
    }

    normal = vec3(0.0, 1.0, 0.0);

    if (fract(intersectPosition.x / 5.0) > 0.5 == fract(intersectPosition.z / 5.0) > 0.5) {
      color = vec3(1.0);
    } else {
      color = vec3(0.0, 0.45, 0.4);
    }
  } else {
   return false;
  }

  return true;
}

void main(void) {
  vec3 cameraDirection = normalize(vPosition - cameraPosition);

  lightDirections[0] = normalize(vec3(0.577350269, 0.577350269, -0.577350269));
  lightDirections[1] = normalize(vec3(0.577350269, 0.577350269, -0.577350269));
  lightDirections[2] = normalize(vec3(0.5, 1.0, 1.0));

  // start pos, normal, end pos
  vec3 position1, normal, position2;
  vec3 color, reflectedColor, colorMax;

  if (intersectWorld(cameraPosition, cameraDirection, position1, normal, reflectedColor)) {
    color = lightAt(position1, normal, -cameraDirection, reflectedColor);
    colorMax = (reflectedColor + vec3(0.7)) / 1.7;
    cameraDirection = reflect(cameraDirection, normal);

    // since integer modulo isn't available
    bool even = true;
    for (int i=0; i<10; i++) {
      // since loops *have* to be unrolled due to no branches
      if (i < int(reflections)) {
        if (even) {
          even = false;
          if (intersectWorld(position1, cameraDirection, position2, normal, reflectedColor)) {
            color += lightAt(position1, normal, -cameraDirection, reflectedColor) * colorMax;
            colorMax *= (reflectedColor + vec3(0.7)) / 1.7;
            cameraDirection = reflect(cameraDirection, normal);
          } else {
            break;
          }
        } else {
          even = true;
          if (intersectWorld(position2, cameraDirection, position1, normal, reflectedColor)) {
            color += lightAt(position2, normal, -cameraDirection, reflectedColor) * colorMax;
            colorMax *= (reflectedColor + vec3(0.7)) / 1.7;
            cameraDirection = reflect(cameraDirection, normal);
          } else {
            break;
          }
        }
      } else {
        break;
      }
    }

    gl_FragColor = vec4(color, 1.0);
  } else {
    gl_FragColor = vec4(0.1, 0.1, 0.1, 1.0);
  }
}

</script>
<script id="vertex-shader" type="x-shader/x-vertex">
    
attribute vec2 aVertexPosition;
attribute vec3 aPlotPosition;

varying vec3 vPosition;

void main(void) {
  gl_Position = vec4(aVertexPosition, 1.0, 1.0);
  vPosition = aPlotPosition;
}

</script>

<script data-main="js/main" src="js/require.js"></script>
</body>
</html>
